// DomainForge Grammar

start: context_definition+

context_definition: "@" IDENTIFIER "{" context_children "}"
context_children: (entity_definition | value_object_definition | event_definition | service_definition | repository_definition | module_definition | role_definition | relationship_definition)*

entity_definition: "#" IDENTIFIER entity_inheritance? "{" entity_children "}"
entity_inheritance: ":" IDENTIFIER
entity_children: (property_definition | method_definition | api_definition | ui_definition)*

value_object_definition: "%" IDENTIFIER "{" property_definition* "}"
event_definition: "^" IDENTIFIER "{" property_definition* "}"
service_definition: ">>" IDENTIFIER "{" service_children "}"
service_children: (method_definition | api_definition)*
repository_definition: "$" IDENTIFIER "{" method_definition* "}"
module_definition: "*" IDENTIFIER "{" module_children "}"
module_children: (entity_definition | value_object_definition | event_definition | service_definition | repository_definition)*
role_definition: "&" IDENTIFIER "{" property_definition* "}"

property_definition: IDENTIFIER ":" type_definition property_default? property_constraint?
property_default: "=" default_value
property_constraint: "[" constraint+ "]"

type_definition: simple_type | generic_type | list_type | dict_type
simple_type: IDENTIFIER
generic_type: IDENTIFIER "<" IDENTIFIER ">"
list_type: "List" "<" type_definition ">"
dict_type: "Dict" "<" type_definition ":" type_definition ">"

method_definition: visibility? IDENTIFIER "(" parameter_list? ")" return_type? method_body?
visibility: VISIBILITY
return_type: ":" type_definition
method_body: "{" description? "}"

parameter_list: parameter ("," parameter)*
parameter: IDENTIFIER ":" type_definition parameter_default?
parameter_default: "=" default_value

default_value: INT | FLOAT | STRING | IDENTIFIER | list_value
list_value: "[" value_list? "]"
value_list: value ("," value)*
value: INT | FLOAT | STRING | IDENTIFIER | list_value

constraint: "required" | "unique" | min_constraint | max_constraint | pattern_constraint | fk_constraint
min_constraint: "min" ":" INT
max_constraint: "max" ":" INT
pattern_constraint: "pattern" ":" STRING
fk_constraint: "foreign_key" ":" IDENTIFIER

relationship_definition: source_entity RELATIONSHIP_SYMBOL target_entity relationship_desc?
relationship_desc: "{" description? "}"
source_entity: IDENTIFIER
target_entity: IDENTIFIER

description: STRING

api_definition: "api" ":" HTTP_METHOD STRING api_params? api_return? api_desc?
api_params: "(" parameter_list? ")"
api_return: ":" type_definition
api_desc: "{" description? "}"

ui_definition: "ui" ":" UI_COMPONENT ui_params? ui_desc?
ui_params: "(" parameter_list? ")"
ui_desc: "{" description? "}"

// Terminals
IDENTIFIER: /[a-zA-Z_][a-zA-Z0-9_]*/
RELATIONSHIP_SYMBOL: "=>" | "<->" | "--" | "->" | "." | "::" | "/" | "="
HTTP_METHOD: "GET" | "POST" | "PUT" | "DELETE" | "PATCH"
UI_COMPONENT: "Form" | "Table" | "Card" | "Detail" | "List"
VISIBILITY: "public" | "private" | "protected"

STRING: /"[^"]*"/

%import common.INT
%import common.FLOAT
%import common.WS

COMMENT: /\/\/[^\n]*/ | /\/\*[\s\S]*?\*\//
%ignore COMMENT
%ignore WS
